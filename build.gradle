group 'cc.xpbootcamp'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def JUNIT5_API_VERSION = '5.3.0'
def JUNIT5_PLATFORM_VERSION = '1.3.0'

ext.libs = [
        junit4: [
                'junit:junit:4.12',
                'org.mockito:mockito-core:2.19.0'
        ],
        junit5: [
                "org.junit.jupiter:junit-jupiter-api:${JUNIT5_API_VERSION}",
                "org.junit.jupiter:junit-jupiter-engine:${JUNIT5_API_VERSION}",
                "org.junit.vintage:junit-vintage-engine:${JUNIT5_API_VERSION}",
                "org.junit.jupiter:junit-jupiter-params:${JUNIT5_API_VERSION}",
                "org.junit.platform:junit-platform-launcher:${JUNIT5_PLATFORM_VERSION}",
                "org.junit.platform:junit-platform-runner:${JUNIT5_PLATFORM_VERSION}",
                'org.mockito:mockito-junit-jupiter:2.17.0'
        ]
]

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = " Result: ${result.resultType} " +
                        "(${result.testCount} Tests, " +
                        "${result.successfulTestCount} Successes, " +
                        "${result.failedTestCount} Failures, " +
                        "${result.skippedTestCount} Skipped) "
                println('\n' + ('-' * output.length()) + '\n' + output + '\n' + ('-' * output.length()))
            }
        }
    }
}

dependencies {
    testCompile libs.junit5
    testCompile 'org.assertj:assertj-core:3.10.0'
    testCompile 'org.hamcrest:hamcrest-core:2.1'
}
